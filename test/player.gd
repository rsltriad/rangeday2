extends CharacterBody3D

@export_group("Movement")
@export var ACCEL := 10
@export var DEACCEL := 30
@export var SPEED := 3.0
@export var SPRINT_MULT := 3
@export var JUMP_VELOCITY := 4.5

@export_group("Sensitivity")
@export var MOUSE_SENSITIVITY := 0.06
@export var STICK_SENSITIVITY := 150

@export_group("Camera")
@export var clamp_max := 1.4
@export var clamp_min := -1.4
@export var bobbing := true
@export var BOB_SPEED := .1
@export var BOB_SIZE := .0025

@export_category("Actions")
@export_group("Move")
@export var move_left_action := "move_left"
@export var move_right_action := "move_right"
@export var move_forward_action := "move_forward"
@export var move_backward_action := "move_backward"
@export var jump_action := "jump"
@export var sprint_action := "sprint"
@export_group("Look")
@export var look_left_action := "look_left"
@export var look_right_action := "look_right"
@export var look_up_action := "look_up"
@export var look_down_action := "look_down"

# Get the gravity from the project settings to be synced with RigidDynamicBody nodes.
var gravity = ProjectSettings.get_setting("physics/3d/default_gravity")
const GameSettings = preload("res://main/settings.gd")

@onready var camera : Camera3D = get_node("Camera3D")
var camera_start_position : Vector3
var dir = Vector3.ZERO
var bob_t := 0.0
var running = false

func _ready():
	Input.set_mouse_mode(Input.MOUSE_MODE_HIDDEN)
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
	camera_start_position = camera.position
	GameSettings.load_cfg()
	MOUSE_SENSITIVITY = GameSettings.sensitivity

func _input(event):
	# Controls player camera with mouse.
	if event is InputEventMouseMotion and Input.get_mouse_mode() == Input.MOUSE_MODE_CAPTURED:
		MOUSE_SENSITIVITY = GameSettings.sensitivity
		camera.rotation.x = camera.rotation.x + clampf(
			deg_to_rad(event.relative.y * MOUSE_SENSITIVITY * -1),
			clamp_min,
			clamp_max,
		)
		self.rotate_y(deg_to_rad(event.relative.x * MOUSE_SENSITIVITY * -1))
	
	else:
		# Release/Grab Mouse for debugging. You can change or replace this.
		if Input.is_action_just_pressed("ui_cancel"):
			if Input.get_mouse_mode() == Input.MOUSE_MODE_VISIBLE:
				Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
			else:
				Input.set_mouse_mode(Input.MOUSE_MODE_VISIBLE)

func _apply_fov():
	# Live-apply the FOV setting when not aiming (fps-hands drives fov during ADS)
	var fh = get_node_or_null("Camera3D/FpsHands")
	if fh == null: return
	fh.start_fov = GameSettings.fov
	if not fh.ads and (fh.weapon == null or fh.weapon.position.is_equal_approx(fh.start_pos)):
		camera.fov = lerpf(camera.fov, GameSettings.fov, 0.2)

func _process(delta):
	_apply_fov()
	# Controls player camera with gamepad
	var look_vector = Input.get_vector(look_right_action,look_left_action,look_down_action,look_up_action)
	#var look_vector = Vector2.ZERO
	#look_vector.x = Input.get_action_strength("look_left") - Input.get_action_strength("look_right")
	#look_vector.y = Input.get_action_strength("look_up") - Input.get_action_strength("look_down")
	
	self.rotate_y(deg_to_rad(look_vector.x) * STICK_SENSITIVITY * delta)
	camera.rotate_x(deg_to_rad(look_vector.y) * STICK_SENSITIVITY * delta)
	camera.rotation.x = clampf(camera.rotation.x, -1.4, 1.4)

func _physics_process(delta):
	var moving = false
	# Add the gravity. Pulls value from project settings.
	if not is_on_floor():
		velocity.y -= gravity * delta

	# Handle Jump.
	if Input.is_action_just_pressed(jump_action) and is_on_floor():
		velocity.y = JUMP_VELOCITY
	
	# This just controls acceleration. Don't touch it.
	var accel
	if dir.dot(velocity) > 0:
		accel = ACCEL
		moving = true
	else:
		accel = DEACCEL
		moving = false


	# Get the input direction and handle the movement/deceleration.
	var input_dir = Input.get_vector(move_left_action, move_right_action, move_forward_action, move_backward_action)
	var direction = (transform.basis * Vector3(input_dir.x, 0, input_dir.y)) * accel * delta
	
	# Run if sprint btn pressed and player moving. Once stopped, disable run
	if Input.is_action_just_pressed(sprint_action) or Input.is_action_pressed(sprint_action):
		running = true
	if direction == Vector3.ZERO:
		running = false
	if running:
		direction = direction * SPRINT_MULT

	if direction:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		velocity.z = move_toward(velocity.z, 0, SPEED)
	
	move_and_slide()
	
	# Push objects
	for i in get_slide_collision_count():
		var collider = get_slide_collision(i).get_collider(0)
		var inertia = 50
		if collider is RigidBody3D:
			if collider.mass < inertia:
				inertia = collider.mass
			collider.apply_central_impulse(-get_slide_collision(i).get_normal()*inertia)
	
	# Bobbing animation
	if bobbing:
		# Horizontal speed only - vertical (jump/fall) velocity must never drive bob,
		# and bob is an absolute offset from the rest position (never accumulated).
		var hvel := Vector2(velocity.x, velocity.z).length()
		if hvel > 0.3 and is_on_floor():
			var speed_k := clampf(hvel / (SPEED/2), 0.0, 6.5)
			bob_t += delta * 60.0 * speed_k * BOB_SPEED
			var bob := Vector3.ZERO
			bob.y = sin(bob_t) * speed_k * BOB_SIZE
			bob.x = cos(bob_t / 2) * speed_k * BOB_SIZE * 2
			camera.position = camera.position.lerp(camera_start_position + bob, minf(12 * delta, 1.0))
		else:
			camera.position = camera.position.lerp(camera_start_position, minf(8 * delta, 1.0))


func _on_fps_hands_give_damage(obj:Node3D, damage:float, point:Vector3):
	var label := Label3D.new()
	label.text = str(int(damage))
	label.pixel_size = .0015
	label.fixed_size = true
	label.no_depth_test = true
	label.position = point
	label.billboard = BaseMaterial3D.BILLBOARD_ENABLED
	#label.look_at_from_position(point, camera.global_position, Vector3(0,1,0), true)
	
	var timer := Timer.new()
	timer.wait_time = .5
	timer.autostart = true
	timer.connect("timeout", label.queue_free)
	
	get_tree().root.add_child(label)
	label.add_child(timer)


func _on_fps_hands_update_ammo(magazine, inventory_ammo, ammo_type):
	$UI/AmmoLabel.text = str(magazine) +"/"+ str(inventory_ammo) +"\n"+ ammo_type


func _on_fps_hands_aiming(ads:bool):
	if !ads:
		$UI/CenterContainer/Crosshair.show()
	else:
		$UI/CenterContainer/Crosshair.hide()
